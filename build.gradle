plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id "com.github.ben-manes.versions" version '0.41.0'
    id 'org.graalvm.buildtools.native' version '0.9.7.1'
    id 'java'
    id 'eclipse'
    id 'jacoco'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.1'
    implementation 'commons-cli:commons-cli:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
}

graalvmNative {
    binaries {
        main {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(17)
                vendor = JvmVendorSpec.matching("GraalVM Community")
            }
            buildArgs.add('--enable-url-protocols=https')
            configurationFileDirectories.from(file('native-config/run'))
            useFatJar = false
            verbose = true
        }
        test {
            javaLauncher = graalvmNative.binaries.main.javaLauncher
        }
    }
}

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v,p -> /($v)/}, replacement: {v, p -> "$v"}]
        pre 'commit'
    }
}

project.version = scmVersion.version
mainClassName = "com.github.otrosien.yamlmerge.YamlMergeApplication"

